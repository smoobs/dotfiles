#!/usr/bin/env perl

use v5.10;

use autodie;
use strict;
use warnings;

use File::Find;
use GD;
use Image::Size;
use JSON;
use List::Util qw( max shuffle sum );
use Getopt::Long;

use constant WIDTH => 2560;

use constant USAGE => <<EOT;
Syntax: $0 [options]

Options:
    -o, --output <file.png>   Output filename
    -w, --width  <width>      Output width in pixels
    -a, --across <n>          Number of images on first line
    -g, --grow   <n>          Growth factor for line width

EOT

my %O = (
  output => "montage.png",
  width  => WIDTH,
  across => undef,
  grow   => 1,
);

GetOptions(
  'output:s' => \$O{output},
  'width:i'  => \$O{width},
  'across:i' => \$O{across},
  'grow:s'   => \$O{grow},
) or die USAGE;

my @image      = ();
my $avg_aspect = 0;
find {
  wanted => sub {
    return unless -f;
    return unless /\.jpg$/i;
    my $name = $_;
    my ( $width, $height ) = imgsize $name;
    return unless defined $width;
    $avg_aspect += $width / $height;
    push @image,
     {name   => $name,
      width  => $width,
      height => $height,
      aspect => $width / $height
     };
  },
  no_chdir => 1
 },
 ".";

$avg_aspect /= @image;

my @img = shuffle @image;
my $use = $O{across} // 1 + int sqrt scalar @img;

my @spec = ();
my $dir  = 1;
while ( $use > 0 && @img >= int $use ) {
  push @spec,
   format_row( sort { $dir * ( $a->{aspect} <=> $b->{aspect} ) }
     splice @img,
    0, int $use );
  $dir = -$dir;
  $use *= $O{grow};
}

my $out_w = $O{width};
my $out_h = sum map { $_->{height} } @spec;

my $im = GD::Image->new( $out_w, $out_h, 1 );
my $y_pos = 0;
for my $row (@spec) {
  my $x_pos = 0;
  for my $ident ( @{ $row->{images} } ) {
    say $ident->{name};
    my $src = GD::Image->new( $ident->{name} );
    my $sw  = 1 + int( $ident->{width} * $row->{height} / $ident->{height} );
    $im->copyResampled( $src, $x_pos, $y_pos, 0, 0, $sw, $row->{height},
      $ident->{width}, $ident->{height} );
    $x_pos += $sw;
  }
  $y_pos += $row->{height};
}

{
  open my $fh, '>', $O{output};
  $fh->print( $im->png );
}

#say JSON->new->pretty->canonical->encode( \@spec );

sub format_row {
  my @img = @_;
  my $height = max map { $_->{height} } @img;
  my $width = 0;
  for my $img (@img) {
    my $iw = $img->{width} * $height / $img->{height};
    $width += $iw;
  }
  my $sh = int( $height * $O{width} / $width );
  return { height => $sh, images => \@img };
}

# vim:ts=2:sw=2:sts=2:et:ft=perl

